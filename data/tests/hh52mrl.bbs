/* 
 * Driver for Hodgkin-Huxley 1952 minimalistic ionic model
 */

/* 
   at the compilation time a flag macro GATE in hh52m.c 
   can be used to decide what to do with gates.

   It should be set on GATE 2 for computation as Markov
   chain variables.
*/
def int neqn 14; /* number of layers of state variables */ 
def real dt 0.01;	    /* time step */
def real DT 1.0;

/* declare schedule variables */
def real begin;
def real end;
def real T;
def real seldom;

/* configuration of the dimensions */
state xmax=1 ymax=1 zmax=1 vmax=neqn+1;

/* Schedule */
k_func name=schedule nowhere=1 pgm={
  T = t*dt;			/* simulation time */
  begin =eq(T, 0);		/* start of simulation [ms] */
  seldom=eq(mod(T,DT),0);	/* output period */
  end   =ge(T, 10.);		/* end of simulation [ms] */
};

/* Reaction substep */
rushlarsen v0=0 v1=neqn-1 ht=dt ionic=hh52m order=totg exp_mc=ntabmrl
  par={ht=dt};

/* define output variable */
def real v;
sample x0=0 v0=0 result=v;
/* ina channel */
def real O_Na;
sample x0=0 v0=4 result=O_Na;
/* ik channel */
def real O_K;
sample x0=0 v0=13 result=O_K;

/* write output to a file */
k_print nowhere=1 when=seldom file="[0].out" append=0
  valuesep="\t" list={T;v;O_K;O_Na;};

/* end simulation */
stop when=end;
end;
