/* Beatbox example bbs scripts FHN using rabbit2012.bbg
 *        
 */ 
// Computation parameters
def real ht  0.001;
def real hx  0.2;
def real D   1.0;
def real bet 0.71;
def real eps 0.30;
def real gam 0.50;
def str u 0; def real umin -2.0; def real umax 2.0;def real umid 0.0;
def str v 1; def real vmin -1.0; def real vmax 1.5;def real vmid 0.5;
def str i 2; 

def str p 3;
def str d 4;

def int neqn [i];

/* Declare global timing variables. In general: 
 * begin is for when the simulation begins, 
 * often, seldom, once, paceout are for output rates (see the k_func device), 
 * end is often used in the examples to set up the stopping criterion using stop device, 
 * stop is the variable used to indicate time for end of simulation not using the stop device. 
 */ 
def real begin;
def real often;
def real end;

state
	geometry=rabbit2012.bbg
	vmax=[i]+1
	normaliseVectors=1
	anisotropy=1
;

/* Assign values to the timing parameters using Beatbox functions for 
 * greater than (ge), less than (le), modulus (mod), etc. 
 */ 
k_func name=timing nowhere=1 pgm={
	begin = eq(t,0);
	often = eq(mod(t,4000),0);
	end   = ge(t,1000000);
};

// Stimulus
k_func when=begin x1=48 pgm={u[u] = 1.7;};
k_func when=begin x0=49 pgm={u[u] = -1.7;};
k_func when=begin y1=48 pgm={u[v] = 0.7;};
k_func when=begin y0=49 pgm={u[v] = -0.7;};

// The computation
diff v0=[u] v1=[i] Dpar=D Dtrans=D/4 hx=hx;
euler v0=[u] v1=[v] ht=ht ode=fhncubpar rest=10000 par={epsu=eps epsv=eps bet=bet gam=gam Iu=@[i]};

ppmout when=often file="ppm/%04d.ppm" mode="w" r=[u] r0=umin r1=umax g=[v] g0=vmin g1=vmax b=[i] b0=0 b1=255;
/* Stopping criterion. This is mandatory.*/ 
stop when=end;

end;
